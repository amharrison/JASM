/*
 * test of mapping pipeline functions.
 * This will test map_retrieve, map_aural, map_visual, and map_association.
 * 
 * pipeline goals merely take the return result after execution and stashes it in
 * the parameter slot. Subsequent pipeline goals can use that value or overwrite it
 * 
 * map_aural and map_visual ignore the current pipeline value and merely specify
 * which slot is assigned to the result via : slotName = =value.
 * 
 * map_retrieval retrieves the current pipeline value and merely specifies which
 * slot is assigned to the result via : slotName = =value
 * 
 * map_association takes the current pipeline value, and uses it as a retrieval
 * spec for an association(current), the spec then defines which slot is assigned
 * to the result from the retrieval buffer.
 * 
 */
 namespace jasm.declarative.MapRetrieval{

import jactr.declarative.*
import jactr.procedural.*
import jactr.retrieval.*
import jactr.imaginal.*
import jactr.visual.*
import jactr.aural.*
import jactr.meta.*
import jactr.goal.*

import jasm.declarative.Goal.*
import jasm.declarative.Pipeline.*

module "org.jactr.core.module.declarative.six.DefaultDeclarativeModule6" [
  "ActivationNoise"          : "0"
  "BaseLevelConstant"        : "0"
  "EnablePartialMatching"    : "false"
  "MaximumDifference"        : "-1"
  "MaximumSimilarity"        : "0"
  "MismatchPenalty"          : "1"
  "PermanentActivationNoise" : "0"]
module "org.jactr.core.module.procedural.six.DefaultProceduralModule6" [
  "DefaultProductionFiringTime" : "0.05"
  "ExpectedUtilityNoise"        : "0"
  "NumberOfProductionsFired"    : "0" ]
module "org.jactr.core.module.goal.six.DefaultGoalModule6" []
module "org.jactr.core.module.retrieval.six.DefaultRetrievalModule6" [
  "EnableIndexedRetrievals" : "false"
  "FINSTDurationTime"       : "3.0"
  "LatencyFactor"           : "1"
  "NumberOfFINSTs"          : "4"
  "RetrievalThreshold"      : "0" ]
module "org.jactr.core.module.meta.MetaModule"[]
module "org.jactr.modules.pm.aural.six.DefaultAuralModule6"[]
module "org.jactr.modules.pm.visual.six.DefaultVisualModule6"[]

/* 
 * common buffers
 */
buffers {
}


chunk buffer-request-hint
chunktype BufferRequest extends BufferGoal{
  hint        = buffer-request-hint
  request     = null // mapping to request with slot value =parameter, null to apply =parameter directly as request
  mapping     = null // mapping to =value or null to return request result directly
  handler     = null // error handler, a subgoal to process error
  end-reached = false
}


/**********************************
 * buffer-request productions
 * 
 **********************************/
production br-BuildRequest-start {
  goal{
    isa BufferRequest
    request =  =initialList
    stage   =  starting
  }
  ?retrieval{
    state != busy
  }
}{
  goal{
    stage = request-request
  }
  +retrieval{
    =initialList
  }
}

/*
 * parameter must be defined
 */
production br-BuildRequest-start-no-spec {
  goal{
    isa BufferRequest
    request   =  null
    parameter =  =initialSpec
    stage     =  starting
  }
  ?retrieval{
    state != busy
  }
}{
  goal{
    request     = =initialSpec
    stage       = request-request
    end-reached = true
  }
  +meta{
    =initialSpec
  }
}


production br-BuildRequest-subsequent{
  goal{
    isa BufferRequest
    stage =  request-request
  }
  retrieval{
    isa ListElement
    item =  =spec
  }
}{
  /*
   * will copy =slotName = =value
   */
  +meta{
    =spec
  }
  +retrieval{
    isa ListElement
    prior =  =retrieval
  }
  goal{}
}["Utility" : "-1"]

production br-BuildRequest-terminal{
  goal{
    isa BufferRequest
    stage =  request-request
  }
  retrieval{
    isa LastListElement
    item =  =spec
  }
}{
  /*
   * will copy =slotName = =value
   */
  +meta{
    =spec
  }
  goal{
    end-reached = true
  }
}

production br-BuildRequest-terminal-error{
  goal{
    isa BufferRequest
    stage =  request-request
  }
  ?retrieval{
    state =  error
  }
}{
  goal{
    end-reached = true
  }
}

production br-FireRequestRetrieval-map-param{
  goal{
    isa BufferRequest
    request     =  =currentSpec
    stage       =  request-request
    buffer      =  "retrieval"
    mapping     =  =mappingSpec
    parameter   =  =parameter
    end-reached =  true
  }
  meta{
    =meta
  }
}{
  +retrieval{
    =meta
  }
  goal{
    stage = actual-request
  }
  +meta{
    =mappingSpec
  }
}

production br-FireRequestRetrieval-map-no-param{
  goal{
    isa BufferRequest
    request     =  =currentSpec
    stage       =  request-request
    buffer      =  "retrieval"
    mapping     =  =mappingSpec
    parameter   =  null
    end-reached =  true
  }
  meta{
    =meta
  }
}{
  +retrieval{
    =meta
  }
  goal{
    stage = actual-request
  }
  +meta{
    =mappingSpec
  }
}

production br-FireRequestRetrieval-no-map-param{
  goal{
    isa BufferRequest
    request     =  =currentSpec
    stage       =  request-request
    buffer      =  "retrieval"
    parameter   =  =parameter
    mapping     =  null
    end-reached =  true
  }
  meta{
    =meta
  }
}{
  +retrieval{
    =meta
  }
  goal{
    stage = actual-request
  }
}

production br-FireRequestRetrieval-no-map-no-param{
  goal{
    isa BufferRequest
    request     =  =currentSpec
    stage       =  request-request
    buffer      =  "retrieval"
    parameter   =  null
    mapping     =  null
    end-reached =  true
  }
  meta{
    =meta
  }
}{
  +retrieval{
    =meta
  }
  goal{
    stage = actual-request
  }
}

/*
 * buffer-request-requested-retrival-null-parameter and -parameter
 * are required in order to bind =parameter where necessary
 */
production br-RequestedRetrieval-finish-no-param{
  goal{
    isa BufferRequest
    request   =  =currentSpec
    stage     =  actual-request
    buffer    =  "retrieval"
    parameter =  null
    mapping   != null
  }
  meta{
    =meta // this is the mapping
  }
  retrieval{
    =meta
  }
}{
  -retrieval{}
  -meta{}
  goal{
    stage  = completed
    return = =value
  }
}

production br-RequestedRetrieval-finish-param{
  goal{
    isa BufferRequest
    request   =  =currentSpec
    stage     =  actual-request
    buffer    =  "retrieval"
    parameter =  =parameter
    mapping   != null
  }
  meta{
    =meta
  }
  retrieval{
    =meta
  }
}{
  -retrieval{}
  -meta{}
  goal{
    stage  = completed
    return = =value
  }
}


production br-RequestedRetrieval-finish-no-map-no-param{
  goal{
    isa BufferRequest
    request   =  =currentSpec
    stage     =  actual-request
    buffer    =  "retrieval"
    parameter =  null
    mapping   =  null
  }
  meta{
    =meta
  }
  retrieval{
    =retrieval
  }
}{
  -retrieval{}
  -meta{}
  goal{
    stage  = completed
    return = =retrieval
  }
}

production br-RequestedRetrieval-finish-no-map-param{
  goal{
    isa BufferRequest
    request   =  =currentSpec
    stage     =  actual-request
    buffer    =  "retrieval"
    parameter =  =parameter
    mapping   =  null
  }
  meta{
    =meta
  }
  retrieval{
    =retrieval
  }
}{
  -retrieval{}
  -meta{}
  goal{
    stage  = completed
    return = =retrieval
  }
}


production br-RequestedRetrieval-fail{
  goal{
    isa BufferRequest
    request =  =currentSpec
    stage   =  actual-request
    buffer  =  "retrieval"
  }
  meta{
    =meta
  }
  ?retrieval{
    state =  error
  }
}{
  -meta{}
  goal{
    stage = build-handler
  }
}

production br-BuildFailHandler-start{
  goal{
    isa BufferRequest
    stage   =  build-handler
    handler =  =handler
  }
}{
  +meta{
    =handler
  }
  goal{
    stage = handler-built
  }
}


/*
 * br-BuildFailHandler-execute 
 * 
 */
production br-BuildFailHandler-execute {
  goal{
    isa BufferRequest
    stage =  handler-built
  }
  meta{
    =meta
  }
}{
  -meta{}
  goal{
    stage = completed
  }
  +goal{
    =meta
    parent =  =goal
  }
}





}
